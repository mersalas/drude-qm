* read crd files and generate Drude PSF
* NB: Water must be last segment generated do to noangle nodihedral
*

DIMENS CHSIZE 5000000 MAXRES 3000000

! Read topology and parameter files
stream toppar_drude.str

!obtain number of segments and segment names
stream step1_reader_nseg.str

set iprot = 0
set icarb = 0

set count 1

label loop_segid
   stream step1_reader_seg@count.str

   ! READ WATER SEPARATELY
   if @type eq water goto continue_segid

   open read unit 10 card name step1_reader_seg@count.crd
   read sequence coor unit 10 RESI  !!!SEGI @segname

   set nterpatch @nter
   if @nterpatch .eq. NTER if @rname .eq. GLY set nterpatch GNTE
   if @nterpatch .eq. NTER if @rname .eq. PRO set nterpatch PROP
   if @nterpatch .eq. ACE  if @rname .eq. PRO set nterpatch ACP

   set cterpatch @cter
   if @cterpatch .eq. CTER if @cname .eq. GLY set cterpatch CTEG
   if @cterpatch .eq. CTER if @cname .eq. PRO set cterpatch CTEP
   if @cterpatch .eq. CNEU if @cname .eq. GLY set cterpatch CNEG
   if @cterpatch .eq. CT1  if @cname .eq. GLY set cterpatch CT1G
   if @cterpatch .eq. CT2  if @cname .eq. GLY set cterpatch CT2G

   set gene = 0
   if @type eq protein then
      !standard protein
      generate @segname first @nterpatch last @cterpatch setup warn drude dmass 0.4 ! show
      set prot@iprot = @segname
      incr iprot by 1
      set gene = 1
   endif

   if @type eq rna then  !UMB: explicitly for rna
      !
      !STANDARD RNA NOT SUPPORTED BY TOPPAR_DRUDE_NA_2017B.STR 
      !
      generate @segname first @nterpatch last @cterpatch setup warn drude dmass 0.4 ! show
      set prot@iprot = @segname
      incr iprot by 1
      set gene = 1
   endif

   if @type eq dna then     !UMB: explicitly for dna
      bomlev -2  !UMB
      !standard dna
      generate @segname first @nterpatch last @cterpatch setup warn drude dmass 0.4 ! show

      define dnaseg sele type C3' .and. segid @segname end
      set totres ?nsel

      set gene = 1
      set cntdna = 1
      label loop_patch_dna
         define cntdna sele ( type C3' .and. segid @segname ) .subset. @cntdna end
         set cntres = ?selresi
         patch deox @segname @cntres setup warn

      incr cntdna by 1
      if cntdna .lt. @totres  goto loop_patch_dna !UMB for last nucleotide it should be different patch

      define cntdna sele ( type C3' .and. segid @segname ) .subset. @cntdna end
      set cntres = ?selresi
      patch deo3 @segname @cntres setup warn      !UMB: deo3 path for terminal nucleotide
      autogenerate angle dihe
      bomlev 0 !UMB

      set prot@iprot = @segname
      incr iprot by 1
   endif

   if @type eq carb then
      generate @segname first none last none setup warn drude dmass 0.4 ! show
      set gene = 1

      set carb@icarb = @segname
      incr icarb by 1
   endif

   if gene .eq. 0 then
      ! default generate if not protein or water
      generate @segname first none last none setup warn drude dmass 0.4 ! show
   endif

   !read coordinates
   rewind unit 10
   read coor card unit 10 append

label continue_segid
incr count by 1
if count le @nsegid goto loop_segid

! PATCHES SHOULD BE APPLIED AFTER GENERATING ALL PSF EXCEPT WATER FOR INTERMOLECULAR LINKAGE
set count 1
label loop_segid2
   stream step1_reader_seg@count.str
   ! patch
   if qpatch .eq. yes then
      stream step1_reader_patch@count.str
   endif
incr count by 1
if count le @nsegid goto loop_segid2

! READ WATER SEPARATELY
set count 1
label loop_segid3
   stream step1_reader_seg@count.str
   if @type .ne. water goto continue_segid2

   open read unit 10 card name step1_reader_seg@count.crd
   read sequence coor unit 10 RESI  !!!SEGI @segname
   generate @segname first none last none setup warn noangle nodihedral drude dmass 0.4 ! show

   rewind unit 10
   read coor card unit 10 append

label continue_segid2
incr count by 1
if count le @nsegid goto loop_segid3

coor sdrude
define ninit sele .not. init .and. hydr end
if ?nsel .gt. 0 then
   ic para
   ic build
   coor sdrude
endif
coor shake
!coor print

nbonds atom vatom switch vswitch bycb -
       ctonnb 10.0 ctofnb 12.0 cutnb 14.0 -
       inbfrq -1 imgfrq -1 wmin 1.0 cdie eps 1.0
energy

!write full coordinate set
open write unit 10 card name step2_drude.psf
write psf card unit 10

open write unit 10 card name step2_drude.pdb
write coor pdb unit 10

open write unit 10 card name step2_drude.crd
write coor card unit 10

coor stat sele all end

open  write unit 90 card name step2_drude.str

set nprot = @iprot
write title unit 90
* set nprot = @nprot
*

if nprot .gt. 0 then
    set iprot = 0
    label write_prot
        write title unit 90
        * set prot@iprot = @prot@@iprot
        *
    incr iprot by 1
    if iprot .lt. @nprot goto write_prot
endif

set ncarb = @icarb
write title unit 90
* set ncarb = @ncarb
*

if ncarb .gt. 0 then
    set icarb = 0
    label write_carb
        write title unit 90
        * set carb@icarb = @carb@@icarb
        *
    incr icarb by 1
    if icarb .lt. @ncarb goto write_carb
endif

stop




























