* PREPARE INPUT FILES FOR OPENMM
*

delete atom sele all end
! Read PSF and coordinates
open read unit 10 card name ../3_drude/step2_drude.psf
read psf unit 10 card

open read unit 10 card name ../3_drude/step2_drude.crd
read coor unit 10 card

define PROT sele none end
if nprot .gt. 0 then
    set iprot = 0
    label loop_prot
        define PROT sele PROT .or. segid @prot@@iprot end
    incr iprot by 1
    if iprot .lt. @nprot goto loop_prot
endif

define CARB sele none end
if ncarb .gt. 0 then
    set icarb = 0
    label loop_carb
        define CARB sele CARB .or. segid @carb@@icarb end
    incr icarb by 1
    if icarb .lt. @ncarb goto loop_carb
endif

define BB sele ( ( type C   .or. type O   .or. type N   .or. type CA  .or. -
		   type P   .or. type O1P .or. type O2P .or. type O5' .or. -
		   type C5' .or. type C4' .or. type C3' .or. type O3' ) .and. PROT ) .or. -
	       ( ( type C+ .or. ( type O5 .and. .bonded. type C1 ) .or. ( type O6 .and. .bonded. type C2 ) ) .and. CARB ) end

set nbb = ?nsel
define SC sele .not. BB .and. .not. hydrogen .and. ( PROT .or. CARB ) end
set nsc = ?nsel

calc nposres = @nbb + @nsc
if nposres .eq. 0 goto skip_rest

open write unit 52 card name ../4_openmm/prot_pos.txt

if nbb .gt. 0 then
   coor stat sele BB end
   set iatom = ?selatom
   
   label ibb
      coor stat sele BB .and. bynu @iatom:?natom end
      set iatom = ?selatom
      calc index0 = @iatom - 1
      
      write title unit 52
      * @index0 BB
      *
      
      incr iatom by 1
   if ?nsel .gt. 1 goto ibb
endif

if nsc .gt. 0 then
   coor stat sele SC end
   set iatom = ?selatom
   
   label isc
      coor stat sele SC .and. bynu @iatom:?natom end
      set iatom = ?selatom
      calc index0 = @iatom
      
      write title unit 52
      * @index0 SC
      *
      
      incr iatom by 1
   if ?nsel .gt. 1 goto isc
endif

label skip_rest

open write unit 10 card name ../4_openmm/step3_charmm2omm.psf
write psf unit 10 card
* CHARMM2OMM PSF
*
close unit 10

open write unit 10 card name ../4_openmm/step3_charmm2omm.oldpsf
write psf unit 10 card oldpsf
* CHARMM2OMM OLD PSF
*
close unit 10

open write unit 10 card name ../4_openmm/step3_charmm2omm.crd
write coor unit 10 card
* CHARMM2OMM CRD
*
close unit 10

open write unit 10 card name ../4_openmm/step3_charmm2omm.pdb
write coor unit 10 pdb
* CHARMM2OMM PDB
* 
close unit 10


























